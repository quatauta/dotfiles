# daniel prompt theme
# vim:syntax=zsh:

prompt_gentoo_daniel_help () {
  cat <<'EOF'
This prompt is color-scheme-able.  You can invoke it thus:

  prompt daniel [<cwdcolor> [<usercolor> [<rootcolor>] [<sshcolor>]]]

EOF
}

prompt_gentoo_daniel_setup () {
  local prompt_cwd=${1:-'yellow'}
  local prompt_user=${2:-'green'}
  local prompt_root=${3:-'red'}
  local prompt_ssh=${4:-'yellow'}

  local prompt_user_style="${prompt_user}"
  local prompt_host_style="gray"

  [ "$EUID" = "0" ] && prompt_user_style="$prompt_root" # logged in as root
  [ "$SSH_TTY" ]    && prompt_host_style="$prompt_ssh"  # connected via ssh

  local base_prompt="%F{$prompt_user_style}%n%b%F{gray}@%F{$prompt_host_style}%m%k"
  local path_prompt="%F{$prompt_cwd}%4~%b%f%k"
  local post_prompt="%b%f%k"

  # git repository details
  autoload prompt_git_status
  local git_status=""
  type prompt_git_status 1>/dev/null 2>&1 && local git_status="\$(prompt_git_status '%f%b%k(' '%f%b%k)' '%B%F{yellow}' '%f%b%k')"

  typeset -g PS1="$base_prompt $path_prompt %F{$prompt_user_style}%(!.#.>)%b%f%k $post_prompt"
  typeset -g PS2="$base_prompt$path_prompt %_> $post_prompt"
  typeset -g PS3="$base_prompt$path_prompt ?# $post_prompt"
  typeset -g RPS1="$git_status"
}

prompt_gentoo_daniel_setup "$@"
